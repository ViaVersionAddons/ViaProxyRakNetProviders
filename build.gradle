plugins {
    id "viaproxy.plugin-conventions"
    id "net.lenni0451.repackager" version "1.0.2"
}

configurations {
    repackagedIncludeInJar

    includeInJar.extendsFrom(repackagedIncludeInJar)
}

repositories {
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroupByRegex "com\\.github\\..+"
        }
    }
}

dependencies {
    implementation("net.raphimc:ViaProxy:3.4.4") {
        transitive = false
    }

    includeInJar "net.java.dev.jna:jna:5.17.0"

    // For dependency sources, see RakNetBackend class
    includeInJar "com.github.RelativityMC.netty-raknet:netty-raknet-client:ae1f415e05"
    includeInJar("com.github.telecran-telecrit:JRakNet:d757e139ce") {
        exclude group: "org.apache.logging.log4j"
        exclude group: "io.netty"
    }
    repackagedIncludeInJar("dev.kastle.netty:netty-transport-raknet:1.2.0") {
        transitive = false
    }
}

dependencyRepackager {
    configuration = configurations.repackagedIncludeInJar
    relocations = [
            "org.cloudburstmc.netty": "dev.kastle.netty"
    ]
    removeEmptyDirs = true
}

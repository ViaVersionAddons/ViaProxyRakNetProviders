plugins {
    id "viaproxy.plugin-conventions"
    id "net.lenni0451.repackager" version "1.0.2"
}

configurations {
    repackagedIncludeInJar

    includeInJar.extendsFrom(repackagedIncludeInJar)
}

repositories {
    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases"

        content {
            includeGroupByRegex "org\\.cloudburstmc(\\..+)?"
        }
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots"

        content {
            includeGroupByRegex "org\\.cloudburstmc(\\..+)?"
        }
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroupByRegex "com\\.github\\..+"
        }
    }
}

dependencies {
    implementation("net.raphimc:ViaProxy:3.4.10-SNAPSHOT") {
        transitive = false
    }

    includeInJar "net.java.dev.jna:jna:5.18.1"

    // For dependency sources, see RakNetBackend class
    includeInJar "com.github.RelativityMC.netty-raknet:netty-raknet-client:15536296c2"
    includeInJar("com.github.telecran-telecrit:JRakNet:d757e139ce") {
        exclude group: "org.apache.logging.log4j"
        exclude group: "io.netty"
    }
    repackagedIncludeInJar("org.cloudburstmc.netty:netty-transport-raknet:1.0.0.CR3-20251031.125212-22") {
        transitive = false
    }
}

dependencyRepackager {
    configuration = configurations.repackagedIncludeInJar
    relocations = [
            "org.cloudburstmc.netty": "org.cloudburstmc.upstream.netty"
    ]
    removeEmptyDirs = true
}
